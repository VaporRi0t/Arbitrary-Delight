plugins {
    id 'fabric-loom' version '1.10.5'
    id 'maven-publish'
}

group = project.mod_group_id
version = project.mod_version

base {
    archivesName = project.mod_id
}


repositories {
    // Farmer's Delight
    maven {
        name = "Greenhouse Maven"
        url = 'https://repo.greenhouse.house/releases/'
    }

    // Porting Lib
    maven { url "https://mvn.devos.one/releases/" }
    maven {
        url "https://maven.jamieswhiteshirt.com/libs-release"
        content {
            includeGroup "com.jamieswhiteshirt"
        }
    }
    maven {
        url "https://jitpack.io/"
        content {
            excludeGroup "io.github.fabricators_of_create"
        }
    }

    // JEI
    maven {
        name = "BlameJared"
        url = "https://maven.blamejared.com/"
    }

}

fabricApi {
    configureDataGeneration()
}

loom {
    runs {
        datagen {
            vmArg "-Dfabric-api.datagen.modid=arbitrarydelight"
            vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated")}"
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"

    // Fabric
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    // Farmer's Delight
    modImplementation("vectorwing:FarmersDelight:1.20.1-2.1.6+refabricated") {
        exclude(group: "net.fabricmc")
    }

    // Porting Lib
    for (String module in port_lib_modules.split(",")) {
        modApi("io.github.fabricators_of_create.Porting-Lib:$module:$port_lib_version")
    }

    // JEI
    modLocalRuntime("mezz.jei:jei-${minecraft_version}-fabric:${jei_version}")

}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.release = 17
}

processResources {
    inputs.property "version", project.version
    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}
